group 'io.cloudgrey.appiumpro'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.appium', name: 'java-client', version: '7.0.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.13.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.5'
    testCompile group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.3.9'
    testImplementation group: 'org.zeroturnaround', name: 'zt-exec', version: '1.10'
    testImplementation group: 'me.xdrop', name: 'fuzzywuzzy', version: '1.2.0'
    testCompile group: 'io.appium', name: 'mitmproxy-java', version: '1.6.1'
    testCompile files('opencv-3.0.0/opencv-300.jar')
   // testCompile files('/usr/local/Cellar/opencv@3/3.4.5_2/share/OpenCV/java/opencv-345.jar')
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3' // remove later
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

test {
    maxParallelForks = 3
    forkEvery = 1
}
buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


//source: https://stackoverflow.com/a/52398377
task marshallClasspathDeps(type: Copy) {
    from sourceSets.test.runtimeClasspath
    // if you need this from the dependencies in the build.gradle then it should be :
    // from sourceSets.main.runtimeClasspath
    include '**/*.class'
    include '**/*.jar'
    exclude { FileTreeElement details ->
        details.file.name.contains('Edition') && !details.file.name.contains('Edition084')
    }
    into 'build/libs/dependency-jars'
}

task marshallOpencvFiles(type: Copy) {
    from file('.')
    include 'opencv-3.0.0/**'
    into 'build/libs'
}

task marshallImageResources(type: Copy) {
    from file('.')
    include 'queryimages/**'
    into 'build/libs'
}

//packaging tests task which is generated from sample using gradle init
task packageTests(type: Jar) {
    from (sourceSets.test.output) {
        include '**/Edition084*'
        include '**/ImageFinder/*'
    }
    classifier = 'tests'
    includeEmptyDirs = false
}

//create zip archive
//source: https://stackoverflow.com/a/36883221/8016330
task zip(type: Zip) {
    dependsOn packageTests,marshallClasspathDeps,marshallOpencvFiles,marshallImageResources
    from 'build/libs/'
    include '*'
    include '*/*' //to include contents of a folder present inside dependency-jars directory
    archiveName 'zip-with-dependencies.zip'
}